apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

if (project.ext.properties.get('isDevBuild') == null) {
    project.ext.isDevBuild = !project.ext.properties.containsKey("buildConfig") || !"release".equals(project.ext.buildConfig)
}

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

project.ext.jenkinsBuildNumber = "$System.env.VERSION_NUMBER"
project.ext.jenkinsJobName = "hub-v2"

group = 'com.flightstats'

version = project.ext.jenkinsBuildNumber

project.ext.isDevBuild = !project.ext.properties.containsKey("buildConfig") || !"release".equals(project.ext.buildConfig)

println "Building version: ${version}"

buildDir = 'build'

mainClassName = "com.flightstats.hub.app.SingleHubMain"

task nasTest(type: Test) {
    exclude 'com/flightstats/hub/dao/aws/**'
    exclude 'com/flightstats/hub/spoke/**'
}

configurations {
    all*.exclude(group: 'org.slf4j', module: 'slf4j-log4j12')
    all*.exclude(group: 'org.slf4j', module: 'slf4j-simple')
    all*.exclude(group: 'javax.ws.rs', module: 'jsr311-api')
    //prevent transitive dependencies from overriding org.eclipse.jetty.aggregate
    all*.exclude(group: 'org.eclipse.jetty')
    all*.exclude(group: 'org.eclipse.jetty.websocket')
    all*.exclude(module: 'log4j')
    integrationTests
}

dependencies {


    /*
    version 9.3.6 of jetty causes some issues with jersey 2.22.1
    try:
        9.2.22.v20170606
            works with 2.25.1
        9.4.6.v20170531
            compile issues with 2.25.1
        9.3.20.v20170531
     */

    compile 'org.eclipse.jetty.aggregate:jetty-all:9.2.22.v20170606'

    //todo update from 2.22.1 -> 2.25.1 seems fine
    compile 'org.glassfish.jersey.containers:jersey-container-jetty-http:2.25.1'
    compile 'org.glassfish.jersey.core:jersey-common:2.25.1'
    compile 'org.glassfish.jersey.media:jersey-media-sse:2.25.1'

    compile 'com.sun.jersey:jersey-client:1.19.4'
    compile 'com.sun.jersey:jersey-core:1.19.4'

    compile 'com.fasterxml.jackson.core:jackson-core:2.8.9'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.8.9'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.9'
    compile 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.8.9'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-guava:2.8.9'

    compile 'com.google.inject:guice:4.1.0'
    compile 'com.google.inject.extensions:guice-servlet:4.1.0'
    compile 'com.google.guava:guava:22.0'
    compile 'com.diffplug.durian:durian:3.4.0'
    compile 'joda-time:joda-time:2.9.9'

    compile 'ch.qos.logback:logback-core:1.2.2'
    compile 'ch.qos.logback:logback-classic:1.2.2'
    compile 'org.slf4j:jul-to-slf4j:1.7.25'
    compile 'org.slf4j:log4j-over-slf4j:1.7.25'
    compile 'org.slf4j:jcl-over-slf4j:1.7.25'

    compile 'de.danielbechler:java-object-diff:0.94'

    compile 'com.amazonaws:aws-java-sdk:1.11.189'
    compile 'org.apache.curator:curator-recipes:2.12.0'
    testCompile 'org.apache.curator:curator-test:2.12.0'
    testCompile 'org.testng:testng:6.11'

    compile 'org.apache.commons:commons-lang3:3.6'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.1'
    compile 'commons-io:commons-io:2.5'
    compile('com.github.rholder:guava-retrying:2.0.0') {
        exclude group: 'com.google.guava'
    }
    compile 'com.google.code.gson:gson:2.8.1'

    compile 'de.jkeylockmanager:jkeylockmanager:2.1.0'

    /*
    we are leaving lombok commented out to prevent it from being a build requirement
    if you want to use lombok, after using and testing annotations,
    before the final merge to master, use delombok to generate the code and remove the annotations

     */

    compile 'org.projectlombok:lombok:1.16.12'
    compile 'com.datadoghq:java-dogstatsd-client:2.3'

    compile 'com.atlassian.commonmark:commonmark:0.9.0'

    testCompile 'com.sparkjava:spark-core:2.3'
    testCompile 'junit:junit:4.+'
    testCompile "org.mockito:mockito-all:1.+"
}


distTar {
    compression = Compression.GZIP
}

applicationDistribution.from('README.md') {
    into "doc"
}

applicationDistribution.from('config/conf') {
    into "conf"
}

applicationDistribution.from('config/bin') {
    into "bin"
}
