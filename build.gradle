apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

if (project.ext.properties.get('isDevBuild') == null) {
    project.ext.isDevBuild = !project.ext.properties.containsKey("buildConfig") || !"release".equals(project.ext.buildConfig)
}

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

project.ext.jenkinsBuildNumber = "$System.env.VERSION_NUMBER"
project.ext.jenkinsJobName = "hub-v2"

group = 'com.flightstats'

version = project.ext.jenkinsBuildNumber

project.ext.isDevBuild = !project.ext.properties.containsKey("buildConfig") || !"release".equals(project.ext.buildConfig)

println "Building version: ${version}"

buildDir = 'build'

mainClassName = "com.flightstats.hub.app.SingleHubMain"

task nasTest(type: Test) {
    exclude 'com/flightstats/hub/dao/aws/**'
    exclude 'com/flightstats/hub/spoke/**'
}

configurations {
    all*.exclude(group: 'org.slf4j', module: 'slf4j-log4j12')
    all*.exclude(group: 'org.slf4j', module: 'slf4j-simple')
    all*.exclude(group: 'javax.ws.rs', module: 'jsr311-api')
    //prevent transitive dependencies from overriding org.eclipse.jetty.aggregate
    all*.exclude(group: 'org.eclipse.jetty')
    all*.exclude(module: 'log4j')
    integrationTests
}

dependencies {

    //version 9.3.6 of jetty causes some issues with jersey 2.22.1
    compile 'org.eclipse.jetty.aggregate:jetty-all:9.2.14.v20151106'

    compile 'org.glassfish.jersey.containers:jersey-container-jetty-http:2.22.1'
    compile 'org.glassfish.jersey.core:jersey-common:2.22.1'
    compile 'org.glassfish.jersey.media:jersey-media-sse:2.22.1'

    compile 'com.sun.jersey:jersey-client:1.19'
    compile 'com.sun.jersey:jersey-core:1.19'

    compile 'com.fasterxml.jackson.core:jackson-core:2.7.0'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.7.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.0'
    compile 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.7.0'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-guava:2.7.0'

    compile 'com.google.inject:guice:4.0'
    compile 'com.google.inject.extensions:guice-servlet:4.0'
    compile 'com.google.guava:guava:19.0'
    compile 'com.diffplug.durian:durian:3.3.0'
    compile 'joda-time:joda-time:2.9.1'

    compile 'ch.qos.logback:logback-core:1.1.3'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'org.slf4j:jul-to-slf4j:1.7.13'
    compile 'org.slf4j:log4j-over-slf4j:1.7.13'
    compile 'org.slf4j:jcl-over-slf4j:1.7.13'
    compile 'net.logstash.logback:logstash-logback-encoder:4.5.1'

    compile 'com.amazonaws:aws-java-sdk:1.10.46'
    compile 'org.apache.curator:curator-recipes:2.9.1'
    testCompile 'org.apache.curator:curator-test:2.9.1'
    testCompile 'org.testng:testng:6.9.10'

    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-io:commons-io:2.4'
    compile 'com.github.rholder:guava-retrying:2.0.0'
    compile 'com.google.code.gson:gson:2.5'
    compile 'com.newrelic.agent.java:newrelic-agent:3.24.1'
    compile 'com.newrelic.agent.java:newrelic-api:3.24.1'
    compile 'org.projectlombok:lombok:1.16.6'

    testCompile 'com.sparkjava:spark-core:2.3'
    testCompile 'junit:junit:4.+'
    testCompile "org.mockito:mockito-all:1.+"
}


distTar {
    compression = Compression.GZIP
}

applicationDistribution.from('README.md') {
    into "doc"
}

applicationDistribution.from('config/triforce') {
    into "triforce"
}

applicationDistribution.from('config/conf') {
    into "conf"
}

applicationDistribution.from('config/bin') {
    into "bin"
}